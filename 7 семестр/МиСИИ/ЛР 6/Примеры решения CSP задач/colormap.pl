%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Решение задачи о раскрашивании карты
%--------------------------------------------------------------------------------
% Карта представляется в виде списка [A::[B,D], B::[C,D], C::[D,E], D::[E]],
% элементами которого являются структуры, образованные оператором «::».
% Каждая структура состоит из имени региона и списка имен смежных регионов.
% Например, A::[B,D] означает, что регион А имеет границы с регионами B и D.
% В предикате раскрасить_карту(Карта,Список_цветов) список Карта обрабатывается
% в порядке следования элементов. При этом цвет очередного региона выбирается
% с помощью предиката select (удалить) из Списка_цветов, а цвета сопряженных
% регионов назначаются из списка оставшихся цветов. Назначенные цвета действуют в
% пределах всего списка Карта, и тем самым ограничивают области определения
% сопряженных переменных. Что приводит в конечном итоге к сокращению ветвей
% дерева  поиска
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%объявление инфиксного оператора
:- op(100,xfy,'::').

раскрасить_карту([Регион::Список_смежных_регионов|Ост_Регионы],Список_цветов):-
  присвоить_цвет(Регион,Список_цветов,Оставшиеся_цвета),
  присвоить_цвета_смежным_регионам(Список_смежных_регионов,Оставшиеся_цвета),
  раскрасить_карту(Ост_Регионы,Список_цветов).
раскрасить_карту([],_).

присвоить_цвет(Регион,Список_цветов,Оставшиеся_цвета):-
	select(Регион,Список_цветов,Оставшиеся_цвета).
присвоить_цвета_смежным_регионам(Список_смежных_регионов,Оставшиеся_цвета):-
	members(Список_смежных_регионов,Оставшиеся_цвета).

% members(L1,L2)проверяет вхождение эл-тов из списка L1 в список L2
% Если L1 не конкретизирован, а L2 конкретизирован, то members будет
% наоборот назначать значения элементам списка L1, выбирая их из L2
members([X|Xs],Ys):-member(X,Ys), members(Xs,Ys).
members([],Ys).

повторять(Цель,1):-Цель.
повторять(Цель,N):-
	not(not(Цель)),	%стирание предыдущих подстановок
	M is N-1,повторять(Цель,M).


 /*-----------------------------------------------------------------------------
Пример вызова:
:-time(повторять(раскрасить_карту([A::[B,D], B::[C,D], C::[D,E], D::[E]],
                 [red, green, blue]),1000)).
Результат:
% 72,105 inferences, 0.031 CPU in 0.023 seconds (136% CPU, 2311043 Lips)
A = C, C = red,
B = E, E = green,
D = blue
------------------------------------------------------------------------------*/


