%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
 % Загадка Эйнштейна — известная логическая задача,
 % по легенде созданная Альбертом Эйнштейном в годы его детства
 %------------------------------------------------------------------------------
 %  На улице стоят пять домов.
 %  Каждый из пяти домов окрашен в свой цвет,
 %  а их жители — люди разных национальностей,
 %  владеют разными животными, пьют разные напитки
 %  и курят разные марки американских сигарет.
 %------------------------------------------------------------------------------
 %  Ограничения:
 %  1. Англичанин живёт в красном доме;
 %  2. У испанца есть собака;
 %  3. В зелёном доме пьют кофе;
 %  4. Русский пьёт чай;
 %  5. Зелёный дом стоит сразу справа от белого дома;
 %  6. Тот, кто курит Old Gold, разводит улиток;
 %  7. В жёлтом доме курят Kools;
 %  8. В центральном доме пьют молоко;
 %  9. Норвежец живёт в первом доме;
 %  10. Сосед того, кто курит Chesterfield, держит лису;
 %  11. В доме по соседству с тем, в котором держат лошадь, курят Kools;
 %  12. Тот, кто курит Lucky Strike, пьёт апельсиновый сок;
 %  13. Японец курит Parliament;
 %  14. Норвежец живёт рядом с синим домом.
 %  Вопрос:
 %  Кто пьёт воду? Кто держит зебру?
 %------------------------------------------------------------------------------
 %  Будем представлять решение в виде списка, состоящего из 5 подсписков
 %  (по количеству домов): Реш =[[1,N1,C1,P1,D1,S1],…,[5,N5,C5,P5,D5,S5]],
 %  где цифра обозначает номер дома, N – национальность,  С- цвет дома,
 %  Р – животное, D – напиток, S - сигареты
 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

эйнштейн:-
	эйнштейн(Решение),
	вывод_табл(Решение).

эйнштейн(Реш) :-
	Реш =[[1,N1,C1,P1,D1,S1],
		[2,N2,C2,P2,D2,S2],
		[3,N3,C3,P3,D3,S3],
		[4,N4,C4,P4,D4,S4],
		[5,N5,C5,P5,D5,S5]],

% 1. Англичанин живет в красном доме
  member([_,англичанин,красный,_,_,_], Реш),
% 2. У испанца есть собака
  member([_,испанец,_,собака,_,_], Реш),
% 3. В зеленом доме пьют кофе
  member([_,_,зеленый,_,кофе,_], Реш),
% 4. Русский пьет чай
  member([_,русский,_,_,чай,_], Реш),
% 5. Зеленый дом стоит сразу справа от белого дома
  member([БД,_,белый,_,_,_], Реш), member([ЗД,_,зеленый,_,_,_], Реш),
  ЗД =:= БД + 1,
% 6. Тот, кто курит Old Gold, разводит улиток
  member([_,_,_,улитки,_,old_gold], Реш),
% 7. В желтом доме курят Kools
  member([_,_,желтый,_,_,kools], Реш),
% 8. В центральном доме пьют молоко
  member([3,_,_,_,молоко,_], Реш),
% 9. Норвежец живет в первом доме
  member([1,норвежец,_,_,_,_], Реш),
% 10.Сосед того, кто курит Chesterfield, держит лису
  member([CД,_,_,_,_,chesterfield], Реш), member([ЛД,_,_,лиса,_,_], Реш),
  (ЛД =:= CД + 1; ЛД =:= CД - 1),
% 11.В доме по соседству с тем, в котором держат лошадь, курят Kools
  member([KД,_,_,_,_,kools], Реш), member([ЛоД,_,_,лошадь,_,_], Реш),
  (ЛоД =:= KД + 1; ЛоД =:= KД - 1),
% 12.Тот, кто курит Lucky Strike, пьёт апельсиновый сок
  member([_,_,_,_,сок,luckystrike], Реш),
% 13.Японец курит Parliament
  member([_,японец,_,_,_,parliament], Реш),
% 14.Норвежец живёт рядом с синим домом
  member([НД,норвежец,_,_,_,_], Реш), member([СД,_,синий,_,_,_], Реш),
  (НД =:= СД + 1; НД =:= СД - 1),
перестановка([норвежец,русский,англичанин,японец,испанец],[N1,N2,N3,N4,N5]),
перестановка([желтый,синий,красный,зеленый,белый],[C1,C2,C3,C4,C5]),
перестановка([лиса,лошадь,улитки,зебра,собака],[P1,P2,P3,P4,P5]),
перестановка([сок,чай,молоко,кофе,вода],[D1,D2,D3,D4,D5]),
перестановка([kools,old_gold,chesterfield,luckystrike,parliament],[S1,S2,S3,S4,S5]).

% вывод решения в форме таблицы
вывод_табл([A,B,C,D,E]) :-
  Н=['N','Национальн.','Цвет дома','Животное','Напиток','Сигареты'],
  write('+--+------------+----------+----------+----------+-------------+'),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',Н),nl,
  write('+--+------------+----------+----------+----------+-------------+'),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',A),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',B),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',C),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',D),nl,
  writef('|%2L|%12L|%10L|%10L|%10L|%13L|',E),nl,
  write('+--+------------+----------+----------+----------+-------------+'),nl.

/* ----------------Решение----------------------------------------------------
Вызов:
:-эйнштейн.
Результат:
+--+------------+----------+----------+----------+-------------+
|N |Национальн. |Цвет дома |Животное  |Напиток   |Сигареты     |
+--+------------+----------+----------+----------+-------------+
|1 |норвежец    |желтый    |лиса      |вода      |kools        |
|2 |русский     |синий     |лошадь    |чай       |chesterfield |
|3 |англичанин  |красный   |улитки    |молоко    |old_gold     |
|4 |испанец     |белый     |собака    |сок       |luckystrike  |
|5 |японец      |зеленый   |зебра     |кофе      |parliament   |
+--+------------+----------+----------+----------+-------------+

------------------------------------------------------------------------------*/
