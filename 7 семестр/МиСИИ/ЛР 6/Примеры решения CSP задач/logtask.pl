%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Поиск решения логической задачи с ограничения типа Alldiff
%--------------------------------------------------------------------------------
%Три друга заняли 1-ое, 2-ое и 3-е места на чемпионате университета.
%Они имеют разные имена: Майкл,Ричард,Саймон.
%Они разных национальностей: американец,австралиец,израильтянин.
%Любят разные виды спорта: баскетбол,крикет,теннис.

%Ограничения:
%   1)Майкл предпочитает баскетбол и играет лучше, чем американец.
%   2)Саймон - израильтянин и играет лучше теннисиста.
%   3)Игрок в крикет занял первое место.
%Решение представляется в виде списка, упорядоченного в соответствии с местами.
%Каждый элемент списка - структура из трех компонент: Имя::Национальность::Спорт,
%где "::" инфиксный оператор, объявляемый в программе.
%Задача: Определить место, имя, национальность, вид спорта для каждого из друзей.
%--------------------------------------------------------------------------------
%Для сравнения вариантов решений следует оценить время выполнения целей:
% 1) :-time(повторять(решить1(Х),1000)).
% 2) :-time(повторять(решить2(Х),1000)).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%объявление инфиксного оператора
:- op(100,xfy,'::').

%Вариант 1: генерация решения , а потом проверка выполнения ограничений
решить1(X) :-генерировать_решение(X), проверить_ограничения(X).

%Вариант 2: проверка выполнения ограничений, а потом генерация решения
решить2(X) :-проверить_ограничения(X), генерировать_решение(X).

%формирование очередного кандидата в решения
%в виде списка [P1::N1::S1, P2::N2::S2, P3::N3::S3]
%Решение-кандидат формируется с помощью  перестановок
%имен Р, национальностей N и видов спорта S. Всего  6*6*6=218 сочетаний
генерировать_решение([P1::N1::S1, P2::N2::S2, P3::N3::S3]):-
  перестановка([P1,P2,P3],[майкл,ричард,саймон]),		%перестановки имен
  перестановка([N1,N2,N3],[американец,австралиец,израильтянин]),	%национальностей
  перестановка([S1,S2,S3],[баскетбол,крикет,теннис]).			%видов спорта

%проверка выполнения ограничений
проверить_ограничения(Решение) :-
	Решение = [X1,X2,X3],
	member(майкл::_::баскетбол,Решение),			%ограничение 1
	предшествует(майкл::_::_,_::американец::_,Решение),	%ограничение 1
	member(саймон::израильтянин::_,Решение),			%ограничение 2
	предшествует(саймон::_::_,_::_::теннис,Решение),	%ограничение 2
	X1 = _::_::крикет.						%ограничение 3

%========предикаты обработки списков============================================

%отношение предшествует(X,Y,L) верно, если  X следует в списке L  раньше  Y
предшествует(X,Y,[X|T]):- member(Y,T).
предшествует(X,Y,[_|T]):- предшествует(X,Y,T).

%перестановка элементов списка
%в начале находим перестановку L1 для хвоста списка L, а
%затем выполняем вставку головы списка H в произвольную позицию L1
перестановка([],[]).
перестановка([X|L],P):-перестановка(L,L1),вставить(X,L1,P).

%вставка элемента Х в список L1
%реализована через удаление X из рез. списка L2
вставить(X,L1,L2):-удалить(X,L2,L1).

%удаление элемента списка: удалить(X,L,L1), где L1- это L без X
удалить(X,[X|T],T).
удалить(X,[Н|T],[Н|T1]):-удалить(X,T,T1).

%===============вспомогательный предикат========================================
% цикл повторения выполнения Цели заданное число раз (N)
повторять(Цель,1):-Цель.
повторять(Цель,N):-
	not(not(Цель)),			%стирание предыдущих подстановок
	M is N-1,повторять(Цель,M).
/*-----------------------------------------------------------------------------
 Примеры вызовов:

:-time(повторять(решить1(Х),1000)).
% 1,179,051 inferences, 0.312 CPU in 0.324 seconds (96% CPU, 3778985 Lips)
Х = [саймон::израильтянин::крикет,
     майкл::австралиец::баскетбол,
     ричард::американец::теннис]

:- time(повторять(решить2(Х),1000)).
% 110,051 inferences, 0.047 CPU in 0.049 seconds (96% CPU, 2351502 Lips)
Х = [саймон::израильтянин::крикет,
     майкл::австралиец::баскетбол,
     ричард::американец::теннис]

Второй вариант выполняется примерно в 6 раз быстрее!
 -------------------------------------------------------------------------------*/
