org 100h            ; Программа начинается с адреса 100h
jmp start           ; Безусловный переход на метку start
;-- Данные ------------------------------------------------------------
v dw 12345
pak db 13, 10, 'Press any key...$'
;----------------------------------------------------------------------
start:
mov bx, v           ; BX = v
mov ah, 2           ; Функция DOS 02h - вывод символа
mov cx, 16          ; Инициализация счётчика цикла
;
lp:
shl bx, 1           ; Сдвиг BX на 1 бит влево
mov dl, '0'         ; dl = '0'
jnc print           ; Переход, если выдвинутый бит равен 0
inc dl              ; dl = dl + 1 = '1'
print:
int 21h             ; Обращение к функции DOS 02h
loop lp             ; Команда цикла
;
mov ah, 9           ; \
mov dx, offset pak  ; > Вывод строки 'Press any key...'
int 21h             ; /
mov ah, 8           ; \
int 21h             ; / Ввод символа без эха
mov ax, 4C00h       ; \
int 21h             ; / Завершение программы
