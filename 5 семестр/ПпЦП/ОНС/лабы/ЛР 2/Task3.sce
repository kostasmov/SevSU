// Задание исходных данных
F = 0.01;   // минимальная частота входного сигнала
R = 100;    // число входов слоя
S1 = 10;    // число нейронов слоя 1
S2 = 1;     // число нейронов слоя 2

// Формирование матрицы входных сигналов
t = 0:0.1:2/F           // отсчёты времени от 0 до 2/F
fi = (2*%pi*F).*t       // аргументы синуса
p = [sin(fi)];          // входные сигналы для 1-го входа
for i = 2:R             // формирование входных сигналов для i-х входов
    p = [p; sin(fi*i)];  
end


// Инициализация весов и смещений для слоя 1
scale = 1/sqrt(R);                          // масштабирование
//scale = 10; 
w1 = grand(S1, R, "unf", -scale, scale);    // вес
b1 = zeros(S1, 1);                          // смещение

// Моделирование слоя 1
n1 = w1*p + repmat(b1, 1, size(p, 2));  // сетевые значения
a1 = ann_purelin_activ(n1);              // активность на выходе 1-го слоя
d_a1 = ann_d_purelin_activ(a1);          // производная активационной функции 

// Вычисление статистик слоя 1
mean_a1 = mean(a1);       // среднее значение функции
stdev_a1 = stdev(a1);     // стандартное отклонение
mean_d_a1 = mean(d_a1);   // среднее значение производной
stdev_d_a1 = stdev(d_a1); // стандартное отклонение


// Инициализация весов и смещений для слоя 2
scale = 1/sqrt(S1)                         // масштабирование
//scale = 10; 
w2 = grand(S2, S1, "unf", -scale, scale);   // вес
b2 = zeros(S2, 1)                           // смещение

// Моделирование слоя 2
n2 = w2*a1 + repmat(b2, 1, size(a1, 2))  // сетевые значения
a2 = ann_purelin_activ(n2);              // активность на выходе 2-го слоя
d_a2 = ann_d_purelin_activ(a2);          // производная активационной функции 

// Вычисление статистик слоя 2
mean_a2 = mean(a2)          // среднее значение функции
stdev_a2 = stdev(a2)        // стандартное отклонение
mean_d_a2 = mean(d_a2)      // среднее значение производной
stdev_d_a2 = stdev(d_a2)    // стандартное отклонение


// Графики первого слоя
clf(1);
figure(1);
subplot(2, 2, 1)
plot(t, a1(1,:), t, a1(2,:));
title('Активность нейронов слоя 1')
subplot(2, 2, 2)
plot(t, d_a1(1,:), t, d_a1(2,:));
title('Производные функции активации слоя 1')
subplot(2, 2, 3)
histplot(20, a1);
title('Гистограмма активности нейронов слоя 1')
subplot(2, 2, 4)
histplot(20, d_a1);
title('Гистограмма производных функции purelin')

// Графики второго слоя
clf(2);
figure(2);
subplot(2, 2, 1)
plot(t, a2(1,:));
title('Активность нейронов слоя 2')
subplot(2, 2, 2)
plot(t, d_a2(1,:));
title('Производные функции активации слоя 2')
subplot(2, 2, 3)
histplot(20, a2);
title('Гистограмма активности нейронов слоя 2')
subplot(2, 2, 4)
histplot(20, d_a2);
title('Гистограмма производных функции purelin')
